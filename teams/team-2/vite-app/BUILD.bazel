load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//teams/team-2/vite-app:vite/package_json.bzl", vite_bin = "bin")
load("@npm//teams/team-2/vite-app:typescript/package_json.bzl", typescript_bin = "bin")
load("@npm//teams/team-2/vite-app:eslint/package_json.bzl", eslint_bin = "bin")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_image_layer", "js_run_devserver")
load("//tools:container_image.bzl", "stamped_container_image")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files", "strip_prefix")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

npm_link_all_packages()

DEPS = [
    "index.html",
    "package.json",
    "tsconfig.app.json",
    "tsconfig.json",
    "tsconfig.node.json",
    "vite.config.ts",
    ":node_modules/vite",
    ":node_modules/@vitejs/plugin-react-swc",
    ":node_modules/react",
    ":node_modules/react-dom",
    ":node_modules/greeting-component",
]

DEV_DEPS = [
    ":node_modules/@types",
]

vite_bin.vite_binary(name = "vite_tool")

filegroup(
    name = "srcs",
    srcs = glob([
        "src/**",
        "public/**",
    ]),
)

vite_bin.vite(
    name = "app",
    srcs = [":srcs"] + DEPS + DEV_DEPS,
    args = [
        "build",
    ],
    chdir = package_name(),
    out_dirs = ["dist"],
)

js_run_devserver(
    name = "dev",
    args = [],
    chdir = package_name(),
    data = [":srcs"] + DEPS + DEV_DEPS,
    tool = ":vite_tool",
)

vite_bin.vite_binary(
    name = "start",
    args = [
        "preview",
    ],
    chdir = package_name(),
    data = [
        ":app",
    ],
)

typescript_bin.tsc_test(
    name = "ts_test",
    size = "small",
    args = [
        "-b",
    ],
    chdir = package_name(),
    data = glob(
        [
            "src/**/*.tsx",
            "src/**/*.ts",
        ],
    ) + [
        "package.json",
        "tsconfig.app.json",
        "tsconfig.json",
        "tsconfig.node.json",
        "vite.config.ts",
    ] + [
        ":node_modules",
    ],
)

eslint_bin.eslint_test(
    name = "eslint_test",
    timeout = "short",
    args = [
        ".",
        "--ext",
        "ts,tsx",
        "--report-unused-disable-directives",
        "--max-warnings",
        "0",
    ],
    chdir = package_name(),
    data = glob(["src/**/*"]) + [
        ".eslintrc.cjs",
        "package.json",
        "tsconfig.app.json",
        "tsconfig.json",
        ":node_modules",
    ],
)

pkg_files(
    name = "pkg_app",
    srcs = [
        ":app",  # produced by vite
    ],
    prefix = "/usr/share/nginx/html",
    strip_prefix = strip_prefix.from_pkg("dist"),
)

pkg_tar(
    name = "tar",
    srcs = [
        ":pkg_app",
    ],
    include_runfiles = True,
    strip_prefix = ".",
    tags = [
        "manual",
        "no-cache",
    ],
)

stamped_container_image(
    name = "vite-app",
    base_image = "@nginx",
    image_layers = [
        ":tar",
    ],
)
