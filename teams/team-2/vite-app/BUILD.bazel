load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//teams/team-2/vite-app:vite/package_json.bzl", vite_bin = "bin")
load("@npm//teams/team-2/vite-app:typescript/package_json.bzl", typescript_bin = "bin")
load("@npm//teams/team-2/vite-app:eslint/package_json.bzl", eslint_bin = "bin")
load("@aspect_rules_js//js:defs.bzl", "js_run_devserver")

npm_link_all_packages()

DEPS = [
    "index.html",
    "package.json",
    "tsconfig.app.json",
    "tsconfig.json",
    "tsconfig.node.json",
    "vite.config.ts",
    ":node_modules/vite",
    ":node_modules/@vitejs/plugin-react-swc",
    ":node_modules/react",
    ":node_modules/react-dom",
]

DEV_DEPS = [
    ":node_modules/@types",
]

vite_bin.vite_binary(name = "vite_tool")

filegroup(
    name = "srcs",
    srcs = glob([
        "src/**",
        "public/**",
    ]),
)

vite_bin.vite(
    name = "app",
    srcs = [":srcs"] + DEPS + DEV_DEPS,
    args = [
        "build",
    ],
    chdir = package_name(),
    out_dirs = ["dist"],
)

js_run_devserver(
    name = "dev",
    args = [],
    chdir = package_name(),
    data = [":srcs"] + DEPS + DEV_DEPS,
    tool = ":vite_tool",
)

typescript_bin.tsc_test(
    name = "ts_test",
    size = "small",
    args = [
        "-b",
    ],
    chdir = package_name(),
    data = glob(
        [
            "src/**/*.tsx",
            "src/**/*.ts",
        ],
        allow_empty = True,
    ) + [
        "package.json",
        "tsconfig.app.json",
        "tsconfig.json",
        "tsconfig.node.json",
        "vite.config.ts",
    ] + [
        ":node_modules",
    ],
)

eslint_bin.eslint_test(
    name = "eslint_test",
    timeout = "short",
    args = [
        ".",
        "--ext",
        "ts,tsx",
        "--report-unused-disable-directives",
        "--max-warnings",
        "0",
    ],
    chdir = package_name(),
    data = glob(["src/**/*"]) + [
        ".eslintrc.cjs",
        "package.json",
        "tsconfig.app.json",
        "tsconfig.json",
        ":node_modules",
    ],
)
